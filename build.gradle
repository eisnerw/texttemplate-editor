apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
    runtime 'org.antlr:antlr4:4.7.2'
}

task generateLexer(type:JavaExec) {
  def lexerName = "TextTemplateLexer"
  inputs.file("$ANTLR_SRC/${lexerName}.g4")
  outputs.file("$GEN_JS_SRC/${lexerName}.js")
  outputs.file("$GEN_JS_SRC/${lexerName}.interp")
  outputs.file("$GEN_JS_SRC/${lexerName}.tokens")
  main = 'org.antlr.v4.Tool'
  classpath = sourceSets.main.runtimeClasspath
  args = ['-Dlanguage=JavaScript', "${lexerName}.g4", '-o', '../../main-generated/javascript']
  workingDir = ANTLR_SRC   
}

task generateColorizeLexer(type:JavaExec) {
  def lexerColorizeName = "TextTemplateColorizeLexer"
  inputs.file("$ANTLR_SRC/${lexerColorizeName}.g4")
  outputs.file("$GEN_JS_SRC/${lexerColorizeName}.js")
  outputs.file("$GEN_JS_SRC/${lexerColorizeName}.interp")
  outputs.file("$GEN_JS_SRC/${lexerColorizeName}.tokens")
  main = 'org.antlr.v4.Tool'
  classpath = sourceSets.main.runtimeClasspath
  args = ['-Dlanguage=JavaScript', "${lexerColorizeName}.g4", '-o', '../../main-generated/javascript']
  workingDir = ANTLR_SRC   
}

task generateParser(type:JavaExec) {
  dependsOn generateLexer
  def lexerName = "TextTemplateLexer"
  def parserName = "TextTemplateParser"
  inputs.file("$ANTLR_SRC/${parserName}.g4")
  inputs.file("$GEN_JS_SRC/${lexerName}.tokens")
  outputs.file("$GEN_JS_SRC/${parserName}.js")
  outputs.file("$GEN_JS_SRC/${parserName}.interp")
  outputs.file("$GEN_JS_SRC/${parserName}.tokens")
  main = 'org.antlr.v4.Tool'
  classpath = sourceSets.main.runtimeClasspath
  args = ['-Dlanguage=JavaScript', "${parserName}.g4", '-no-listener', '-visitor', '-o', '../../main-generated/javascript']
  workingDir = ANTLR_SRC
}
